module gen/kotlin/type

imports

  libspoofax/stratego/debug

  signatures/-
  signatures/lex/-
  signatures/impl/-

  statsem/program
  
  gen/idtype-tuples
  gen/util

rules

  p2k-type-sig: UnitTy() -> "Unit"
  p2k-type-sig: BoolTy() -> "Boolean"
  p2k-type-sig: IntTy()  -> "Int"
  p2k-type-sig: StrTy()  -> "String"
  p2k-type-sig: PathTy() -> "PPath"
  p2k-type-sig: TopTy()  -> "Any"

  p2k-type-sig:
    DataTy(occ) -> javaClassId
    where 
      JavaTypeKind() := <pie-prop(|"Type", "kind")> occ
    ; <pie-prop(|"Type", "mods"); not(collect-one(?Transient()))> occ
    with  
      javaClassId := <pie-prop(|"Type", "javaClassId")> occ

  p2k-type-sig:
    DataTy(occ) -> $[OutTransient<[javaClassId]>]
    where 
      JavaTypeKind() := <pie-prop(|"Type", "kind")> occ
    ; <pie-prop(|"Type", "mods"); collect-one(?Transient())> occ
    with  
      javaClassId := <pie-prop(|"Type", "javaClassId")> occ


  p2k-type-sig: 
    TupleTy(tys) -> $[Tuple[length]<[tyStr]>]
    with
      length := <length> tys
    ; tyStr  := <map(p2k-type-sig); pie-concat-comma> tys

  p2k-type-sig: ListTy(ty)     -> $[ArrayList<[<p2k-type-sig> ty]>]
  p2k-type-sig: NullableTy(ty) -> $[[<p2k-type-sig> ty]?]
  
rules

  p2k-type-generate-dataclass(|name):
    idTypeTuples -> 
$[data class [name]([params]) : [tupleSig] {
  constructor(tuple: [tupleSig]): this([constructorCall])
}]
    with
      paramStrs       := <map(\(typeId, ty) -> $[val [typeId] : [<p2k-type-sig> ty]]\)> idTypeTuples
    ; params          := <pie-sep-comma; concat-strings> paramStrs
    ; tys             := <map(Snd)> idTypeTuples
    ; tupleSig        := <p2k-type-sig> TupleTy(tys)
    ; constructorCall := <range; map(\i -> $[tuple.component[i]()]\); pie-concat-comma> (1, <length; inc> tys)

