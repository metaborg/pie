module mb:pie:example:parse

func normalize(file: path, includeDirs: path*) -> path = {
  requires file;
  [requires dir with extension "sdf" | dir <- includeDirs];
  val normFile = file.replaceExtension("norm");
  val depFile = file.replaceExtension("dep");
  exec(["sdf2normalized"] + "$file" + ["-I$dir" | dir <- includeDirs] + "-o$normFile" + "-d$depFile");
  [requires dep by hash | dep <- extract-deps(depFile)];
  generates normFile;
  normFile
}
func extract-deps(depFile: path) -> path* = foreign java ex.Api#extractSdfDeps
func generate-table(normFiles: path*, outputFile: path) -> path = {
  [requires file by hash | file <- normFiles];
  exec(["sdf2table"] + ["$file" | file <- normFiles] + "-o$outputFile");
  generates outputFile;
  outputFile
}

func exec(arguments: string*) -> (string, string) = foreign java mb.pie.runtime.builtin.process#execute


data Ast = foreign java ex.Ast {}
data Token = foreign java ex.Token {}
data Msg = foreign java ex.Msg {}
data ParseTable = foreign java ex.ParseTable {}
func table2object(text: string) -> ParseTable = foreign ex.pie.Table2Object
func parse(text: string, table: ParseTable) -> (Ast, Token*, Msg*) = foreign ex.pie.Parse

data Styling = foreign java ex.Styling {}
func style(tokenStream: Token*) -> Styling = foreign ex.pie.Style

func update-editor(text: string) -> (Styling, Msg*) = {
  val sdfFiles = [./lexical.sdf, ./contextfree.sdf];
  val normFiles = [normalize(file, [./include]) | file <- sdfFiles];
  val parseTableFile = generate-table(normFiles, ./parse.tbl);
  val parseTableContents = (read parseTableFile)!;
  val parseTable = table2object(parseTableContents);
  val (ast, tokenStream, msgs) = parse(text, parseTable);
  val styling = style(tokenStream);
  (styling, msgs)
}
