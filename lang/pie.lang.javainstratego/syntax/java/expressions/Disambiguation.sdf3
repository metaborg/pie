module java/expressions/Disambiguation

imports
  java/expressions/ClassInstanceCreation
  java/expressions/Postfix
  java/expressions/UnaryOperators
  java/expressions/BinaryOperators
  java/expressions/ArrayAccess
  java/expressions/FieldAccess
  java/expressions/MethodInvocation
  java/expressions/AssignmentOperators
  java/expressions/LambdaExpressions
  java/names/Names
  
context-free priorities

  FieldAccess.Field > 
  Expression = ExpressionName,
  
  ElementValue = Expression <0> >
  Expression.Assign  

context-free priorities

  Expression.QualifiedInstance > 
  {right: 
    Expression.PostIncr 
    Expression.PostDecr } >
  { 
    Expression.CastPrimitive 
    Expression.CastReference }

context-free priorities
  
  Expression.CastPrimitive > 
  {left: 
    Expression.Mul 
    Expression.Div
    Expression.Mod }
  
context-free priorities
  
  Expression.CastReference > 
  { Expression.PreIncr
    Expression.PreDecr
    Expression.Plus
    Expression.Minus }
  ,
  Expression.CastReference <4> .> 
  { Expression.Plus
    Expression.Minus }   
    
 
context-free priorities
  
  { ArrayAccess.ArrayAccess 
    FieldAccess.Field 
    Expression.InvokeQExp } <0> >
  {right: 
    Expression.PostIncr 
    Expression.PostDecr } >
  { Expression.PreIncr 
    Expression.PreDecr 
    Expression.Plus 
    Expression.Minus 
    Expression.Complement 
    Expression.Not } >
  {left:
     Expression.Mul 
     Expression.Div 
     Expression.Mod } >
  {left: 
    Expression.Add 
    Expression.Sub } >
  {left: 
    Expression.LeftShift 
    Expression.RightShift 
    Expression.URightShift } >
  {left: 
    Expression.InstanceOf 
    Expression.Lt 
    Expression.Gt 
    Expression.LtEq 
    Expression.GtEq } >
  {left: 
    Expression.Eq 
    Expression.NotEq } >
  Expression.And >
  Expression.Xor >
  Expression.Or >
  Expression.LazyAnd >
  Expression.LazyOr >
  Expression.Cond >
  {right: 
    Expression.Assign
    Expression.AssignMul
    Expression.AssignDiv
    Expression.AssignMod
    Expression.AssignAdd
    Expression.AssignSub
    Expression.AssignLeftShift
    Expression.AssignRightShift
    Expression.AssignURightShift
    Expression.AssignAnd
    Expression.AssignXor
    Expression.AssignOr
    Expression.LambdaExpression }

lexical restrictions

  "+" -/- [\+]
  "-" -/- [\-]
  "/" -/- [\/]
