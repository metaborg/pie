module java/expressions/ClassInstanceCreation

// 15.9. Class Instance Creation Expressions

imports
  java/names/Names
  java/lexical/Identifiers
  java/interfaces/Annotations
  java/types/ParameterizedTypes
  java/classes/ClassDeclarations

context-free syntax
  
  Expression                   = UnqualifiedInstance
  Expression.QualifiedInstance = <<Expression>.<UnqualifiedInstance>>
  
  UnqualifiedInstance.NewInstance = <
  new <TypeArguments?> <{Annotation " "}*> <Id> <QualifiedId*> <TypeArgumentsOrDiamond?>(<{Expression ","}*>) >
  
  UnqualifiedInstance.NewInstance = <  
  new <TypeArguments?> <{Annotation " "}*> <Id> <QualifiedId*> <TypeArgumentsOrDiamond?>(<{Expression ","}*>) {
    <{ClassBodyDeclaration "\n"}*>
  }>
  
  QualifiedId.QualifiedId = <.<{Annotation " "}*> <Id>>
  
  TypeArgumentsOrDiamond         = TypeArguments
  TypeArgumentsOrDiamond.Diamond = [<>]

template options  
  
  tokenize : "<" 