module java/classes/ConstructorDeclarations

// 8.8. Constructor Declarations

imports
  java/classes/ClassDeclarations
  java/lexical/Identifiers
  java/classes/MethodDeclarations
  java/types/ParameterizedTypes
  java/names/Names
  java/interfaces/Annotations
  java/expressions/Main
  java/statements/Blocks

context-free syntax
  
  ConstructorDeclaration.ConstrDecl = <
  <{ConstructorModifier " "}*> <TypeParameters?> <Id>(<FormalParams>) <Throws?>{
    <ConstructorInvocation?>
    <{BlockStatement "\n"}*>
  }>
  
  ConstructorModifier           = Annotation
  ConstructorModifier.Public    = "public"
  ConstructorModifier.Private   = "private"
  ConstructorModifier.Protected = "protected"
  
  ConstructorInvocation.AltConstrInv      = <<TypeArguments?>this(<{Expression ", "}*>);>
  ConstructorInvocation.SuperConstrInv    = <<TypeArguments?>super(<{Expression ", "}*>);>
  ConstructorInvocation.ExprNameConstrInv = <<Expression>.<TypeArguments?>super(<{Expression ", "}*>);>