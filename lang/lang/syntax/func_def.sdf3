module func_def

imports

  lex/literal
  type
  common
  func_body

template options

  keyword -/- [a-zA-Z0-9\-\_]

context-free sorts

  FuncHead
  ContextParams
  Param
  Params
  FuncDef
  FuncImpl
  PieKeyFunc

context-free syntax // Function head

  FuncHead.FuncHead = [[FUNCID][TypeParams]([Params])[ContextParams] -> [Type]]

  ContextParams.NoContextParams = []
  ContextParams.ContextParams = <[<Params>]>

  Param.Param   = <<Type>>
  Param.NParam  = <<VALID> : <Type>> {prefer} // prefer this over a Param(ImportTy(...))
  Params.Params = <<{Param ", "}*>>

context-free syntax // Function definition

  FuncDef.FuncDef              = <func <FuncHead> = <FuncImpl>>
  FuncImpl.PieForeignFuncImpl  = <foreign <PIEQTYPEID>>
  FuncImpl.JavaForeignFuncImpl = <foreign java <JAVAQTYPEID>#<JAVAFUNCID>>
  FuncImpl.JavaForeignConstructorImpl = <foreign java constructor <JAVAQTYPEID>>
  FuncImpl.PieFuncImpl         = <<PieFuncBody><PieKeyFunc?>>
  
  PieKeyFunc.PieKeyFunc = <@ <PieFuncBody>>
