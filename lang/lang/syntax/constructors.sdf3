module constructors

imports

  lex/literal
  pie
//  statix/API // use for Stx-Scope

// A semantic type (as opposed to the lexical, non-all caps Type)
context-free start-symbols TYPE // For type hint on editor mouseover
context-free sorts TYPE UpperBoundType LowerBoundType CaptureState GenericParamType
context-free syntax

  UpperBoundType.NoUpperBoundType = < [no upper bound]>
  UpperBoundType.UpperBoundType = < : <TYPE>>

  LowerBoundType.NoLowerBoundType = < [no lower bound]>
  LowerBoundType.LowerBoundType = < -: <TYPE>>

  TYPE.UnitType = [unit]
  TYPE.BoolType = [bool]
  TYPE.IntType = [int]
  TYPE.StrType = [string]
  TYPE.PathType = [path]
  TYPE.NullType = [null]
  TYPE.TopType = [TOP]
  TYPE.BottomType = [BOTTOM]

  TYPE.NullableType = [[TYPE]?]
  TYPE.ListType = [[TYPE]*]
  TYPE.EmptyListType = [ANY*]
  TYPE.TupleType = [([{TYPE ", "}+])]
  TYPE.SupplierType = [supplier([TYPE])]

  GenericParamType.GenericParamType = [[TYPEID][UpperBoundType][LowerBoundType]]
  TYPE.FuncType = [func<[{GenericParamType ", "}*]>([{TYPE ", "}*]) -> [TYPE]]

//  TYPE.DataType = [data: [Stx-Scope]] // doesn't work, defined in Statix and Stratego for now
//  TYPE.GenericParameter2TYPE = GenericParameter

  CaptureState.Free = [free]
  CaptureState.Bound = [bound]
  TYPE.WildcardType = [WILDCARD[UpperBoundType][LowerBoundType] (capture state: [CaptureState])]

// Boolean values. Used in Statix logic.
context-free sorts BOOLEAN
context-free syntax

  BOOLEAN.TRUE = [TRUE]
  BOOLEAN.FALSE = [FALSE]

// Import sorts and constructors. Used by Statix to handle imports.
context-free sorts Renaming NormalizedImport
context-free syntax

  Renaming.Renaming = [[{STRING ":"}*] -> [{STRING ":"}*]]
  NormalizedImport.NormalizedImport = [normalized import: [Renaming]]

// The kind of an Assignability check.
// Used by Statix to discern different assignability checks.
context-free sorts AssignableKind
context-free syntax

  AssignableKind.InvariantKind = [assignability check for invariant types]
  AssignableKind.TypeArgKind = [assignability check for type argument types]
  AssignableKind.ExpressionKind = [assignability check for expression types]

// The kind of a Function. Used by code generation.
context-free sorts FuncImplKind
context-free syntax

  FuncImplKind.JavaForeignFuncKind = [foreign java func]
  FuncImplKind.JavaForeignConstructorKind = [foreign java constructor]
  FuncImplKind.PieForeignFuncKind = [foreign pie func]
  FuncImplKind.PieFuncKind = [pie func]

// The kind of a value. Used by code generation.
context-free sorts ValKind
context-free syntax

  ValKind.LocalValKind = [local val]
  ValKind.InputValKind = [input val]

// The kind of a data type.
context-free sorts TypeKind
context-free syntax

  TypeKind.JavaTypeKind = [java type]

// context values that are saved in the scope graph
context-free sorts CONTEXT
context-free syntax

  CONTEXT.ContextType = [context type: [TYPE]]
  CONTEXT.ContextName = [context name: [STRING]]
  CONTEXT.ContextModule = [context module: { [Module] }]

lexical sorts STRING
lexical syntax STRING = ~[]*
