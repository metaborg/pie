module gen/java-ast/type

imports

  libspoofax/stratego/debug

  src-gen/signatures/-
  signatures/-

  statsem/pie

  gen/idtype-tuples
  gen/util
  gen/java-ast/ast-util

rules

  p2j-ast-type-sig: UnitTy() -> ClassType([], Id("None"), None())
  p2j-ast-type-sig: BoolTy() -> ClassType([], Id("Boolean"), None())
  p2j-ast-type-sig: IntTy()  -> ClassType([], Id("Integer"), None())
  p2j-ast-type-sig: StrTy()  -> ClassType([], Id("String"), None())
  p2j-ast-type-sig: PathTy() -> ClassType([], Id("FSPath"), None())
  with
    <add-import> "mb.resource.fs.FSPath"
  p2j-ast-type-sig: TopTy()  -> ClassType([], Id("Object"), None())
  p2j-ast-type-sig: NullTy() -> ClassType([], Id("Void"), None())

  p2j-ast-type-sig:
    DataTy(occ) -> ClassType([], Id(javaClassId), None())
    where
      JavaTypeKind() := <pie-prop(|"Type", "kind")> occ
    ; <pie-prop(|"Type", "mods"); not(collect-one(?Transient()))> occ
    with
      javaClassId := <pie-prop(|"Type", "javaClassId")> occ

    p2j-ast-type-sig:
    ImportTy(_, occ) -> ClassType([], Id(javaClassId), None())
    where
      JavaTypeKind() := <pie-prop(|"Type", "kind")> occ
    ; <pie-prop(|"Type", "mods"); not(collect-one(?Transient()))> occ
    with
      javaClassId := <pie-prop(|"Type", "javaClassId")> occ

  p2j-ast-type-sig:
    DataTy(occ) -> ClassType(
      [],
      Id("OutTransient"),
      Some(TypeArguments([ClassType([], Id(javaClassId), None())]))
    )
    where
      JavaTypeKind() := <pie-prop(|"Type", "kind")> occ
    ; <pie-prop(|"Type", "mods"); collect-one(?Transient())> occ
    with
      javaClassId := <pie-prop(|"Type", "javaClassId")> occ
    ; <add-import> "mb.pie.api.OutTransient"

  p2j-ast-type-sig:
    TupleTy(pieTypes) -> ClassType([], Id($[Tuple[length]]), Some(TypeArguments(javaTypes)))
    with
      length    := <length> pieTypes
    ; javaTypes := <map(p2j-ast-type-sig)> pieTypes
    ; <add-import> $[mb.pie.util.Tuple[length]]
    ; <add-import> "mb.pie.util.Util"

  p2j-ast-type-sig:
    ListTy(ty) -> ClassType([], Id("ArrayList"), Some(TypeArguments([<p2j-ast-type-sig> ty])))
    with
      <add-import> "java.util.ArrayList"
  p2j-ast-type-sig: NullableTy(ty) -> <p2j-ast-type-sig> ty // Java types are always nullable
  
  p2j-ast-type-sig:
    SupplierTy(ty) -> ClassType([], Id("Supplier"), Some(TypeArguments([<p2j-ast-type-sig> ty])))
    with
      <add-import> "mb.pie.api.Supplier"

rules

  // p2j-ast-type-generate-dataclass(|name):
  //   list of (varId, Pie type) -> Java (data)class declaration AST node
  p2j-ast-type-generate-dataclass(|name):
    idPieTypeTuples -> result
    with
      <add-import> "org.checkerframework.checker.nullness.qual.Nullable"
    ; <add-import> "java.io.Serializable"
    ; <add-import> $[mb.pie.util.Tuple[<length> idPieTypeTuples]]
    ; <add-import> $[mb.pie.util.Util]
    ; tupleTy        := $[Tuple[<length> idPieTypeTuples]]
    ; tys            := <map(Snd)> idPieTypeTuples
    ; types          := <map(p2j-ast-type-sig)> tys
    ; idTypeTuples   := <zip> (<map(Fst)> idPieTypeTuples, types)
    ; getters        := <nmap(type-tuple-to-ast-getter | 1)> idTypeTuples
    ; params         := <create-ast-formal-params> idTypeTuples
    ; callParams     := <map(type-tuple-to-ast-call-param)> idTypeTuples
    ; tupleNumParams := <nmap(type-tuple-to-ast-tuple-field-get | 1)> idTypeTuples
    ; tupleTypes     := <map(type-tuple-to-type)> idTypeTuples
    ; result         := ClassDeclaration(
        [Public(), Static(), Final()]
      , Id(name)
      , None()
      , Some(SuperClass(ClassType([], Id(tupleTy), Some(TypeArguments(tupleTypes)))))
      , Some(SuperInterface([ClassType([], Id("Serializable"), None())]))
      , <concat> [
          [
            ConstrDecl(
              [Public()]
            , None()
            , Id(name)
            , params
            , None()
            , Some(SuperConstrInv(None(), callParams))
            , []
            )
          , MethodDecl(
              [MarkerAnno(TypeName(Id("Nullable"))), Public(), Static()]
            , MethodHeader(
                ClassType(Id(name), None())
              , Id("fromTuple")
              , SingleParam(FormalParam(
                  [MarkerAnno(TypeName(Id("Nullable")))]
                , ClassType(Id(tupleTy), Some(TypeArguments(tupleTypes)))
                , VariableDecl(Id("tuple"))
                ))
              , []
              , None()
              )
            , Block([
                If(Eq(ExpressionName(Id("tuple")), Null()), Return(Null()))
              , Return(NewInstance(None(), [], Id(name), [], None(), tupleNumParams))
              ])
            )
          ]
        , getters
        ]
      )
      // Note: meaning of this AST:
      //   public static final class [name] extends [tupleTy]<[tupleTypes]> implements Serializable {
      //     public [name]([params]) {
      //       super([callParams]);
      //     }
      //
      //     @Nullable
      //     public static [name] fromTuple(@Nullable [tupleTy]<[tupleTypes]> tuple) {
      //       if (tuple == null) {
      //         return null;
      //       }
      //       return new [name]([tupleNumParams]);
      //     }
      //     [getters]
      //   }

  // create-formal-params: list of (varId, Java type) -> params
  // Converts a list of type tuples into the correct FormalParamaters constructor
  // (NoParams, SingleParam or ParamList)
  create-ast-formal-params: idTypeTuples -> NoParams()
  where
    0 := <length> idTypeTuples

  create-ast-formal-params: idTypeTuples -> SingleParam(<type-tuple-to-ast-formal-param> idTypeTuple)
  where
    1 := <length> idTypeTuples
  with
    [idTypeTuple] := idTypeTuples

  create-ast-formal-params: idTypeTuples -> result
  where
    <gt> (<length> idTypeTuples, 1)
  with
    rest   := <map(type-tuple-to-ast-formal-param)> <take(|<dec> <length> idTypeTuples)> idTypeTuples
  ; last   := <type-tuple-to-ast-formal-param> <last> idTypeTuples
  ; result := ParamList(rest, last)

  type-tuple-to-ast-field:
    (varId, ty) -> FieldDecl([Public(), Final()], <java-classType-to-unannType> ty, [varId])
    // Note: meaning of this AST: public final [ty] [varId];
  type-tuple-to-type: (varId, ty) -> ty
  type-tuple-to-ast-tuple-field-get(|number):
    _ -> InvokeQExp(ExpressionName(Id("tuple")), None(), Id($[component[number]]), [])
    // Note: meaning of this AST: tuple.component[number]()
  type-tuple-to-ast-getter(|number): (varId, ty) -> MethodDecl(
      [Public(), Final()]
    , MethodHeader(<java-classType-to-unannType> ty, Id($[get_[varId]]), NoParams(), [], None())
    , Block([Return(Invoke(MethodName(Id($[component[number]])), []))])
    )
    // Note: meaning of this AST:
    //   public final [ty] get_[varId]() {
    //     return component[number]();
    //   }
  type-tuple-to-ast-formal-param:
    (varId, ty) -> FormalParam([], <java-classType-to-unannType> ty, VariableDecl(Id(varId)))
    // Note: meaning of this AST: [ty] [varId]
  type-tuple-to-ast-call-param: (varId, ty) -> ExpressionName(Id(varId))
  type-tuple-to-ast-assignment:
    (varId, ty) -> ExpressionStatement(Assign(Field(This(), Id(varId)), ExpressionName(Id(varId))))
    // Note: meaning of this AST: this.[varId] = [varId]
