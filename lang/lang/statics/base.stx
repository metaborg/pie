module statics/base

imports

  signatures/constructors-sig
  signatures/lex/literal-sig
  signatures/pie-sig
  statics/common
  statics/type

// These sorts/constructors are declared here and not in
// syntax/constructors.sdf3 because I couldn't figure out how to declare a
// scope in SDF3 so that it generates constructors with Statix scopes.
signature
  sorts
    ModuleTree
    MODULE
    ParameterizedType
    TypeParameter

  constructors
    /**
     * ModuleTreeRoot(tree_scopes, subnodes)
     * The root of a module tree. Contains ModuleTreeNodes in [subnodes].
     * The scopes [tree_scopes] represent scopes to be imported into this
     * module tree.
     * tree_scopes: A list of names and tree scopes that represent the same
     *              submodule. Used to point import module tree to root module
     *              tree on module imports. Each scope will be pointed to with
     *              an I edge.
     * subnodes:    The nodes representing the submodules of this module.
     *              This should be a list of only ModuleTreeNodes without
     *              duplicate names, anything else is a bug in this Statix
     *              specification.
     */
    ModuleTreeRoot : list(scope) * list(ModuleTree) -> ModuleTree

    /**
     * ModuleTreeNode(name, file_scopes, tree_scopes, subnodes)
     * A node of a module tree. Each node represents a module and its contents.
     * name:        The name of the submodule.
     * file_scopes: A list of file scopes that are instances of this submodule.
     *              Each scope will be pointed to with a FILE edge.
     *              This should have 0 or 1 scope, more represents a duplicate
     *              module error.
     * tree_scopes: A list of names and tree scopes that represent the same
     *              submodule. Used to point import module tree to root module
     *              tree on module imports. Each scope will be pointed to with
     *              an I edge.
     * subnodes:    The nodes representing the submodules of this module.
     *              This should be a list of only ModuleTreeNodes without
     *              duplicate names, anything else is a bug in this Statix
     *              specification.
     */
    ModuleTreeNode : MODID * list(scope) * list((MODID * scope)) * list(ModuleTree) -> ModuleTree

    /**
     * MODULE(module_scope, qualified_name)
     * Data class representing module [qualified_name] and its scope
     * [module_scope].
     * A MODULE holds the same data as a SUBMODULE, but a MODULE signifies to
     * module.stx!addToModuleTree that this module should be added within the
     * new tree.
     * Also see the constructors for ModuleTree (ModuleTreeRoot and
     * ModuleTreeNode) above.
     * module_scope:   scope that this module represents.
     * qualified_name: qualified name of this module.
     */
    MODULE : scope * list(MODID) -> MODULE

    /**
     * SUBMODULE(module_scope, qualified_name)
     * Data class representing module [qualified_name] and its scope
     * [module_scope].
     * A SUBMODULE holds the same data as a MODULE, but a SUBMODULE signifies
     * to module.stx!addToModuleTree that this module should refer to the same
     * module in the root tree.
     * Also see the constructors for ModuleTree (ModuleTreeRoot and
     * ModuleTreeNode) above.
     * module_scope:   scope that this module represents.
     * qualified_name: qualified name of this module.
     */
    SUBMODULE : scope * list(MODID) -> MODULE

    DataType : scope -> TYPE
    TypeParameter : scope * TYPEID -> TypeParameter // name + scope it is defined in
    TypeParameter2TYPE : TypeParameter -> TYPE

  relations
    root : scope
    file : scope
    mod_wip : MODULE
    mod : MODID -> scope
    is_proxy_module : // no value, can be set as a flag
    val : VALID -> TYPE
    func : FUNCID -> (scope * TYPE) // function scope, function type
    func_local : FUNCID -> (scope * TYPE)  // function scope, function type
    data : TYPEID -> scope
    data_local : TYPEID -> scope
    // todo: change generic_params to list(TypeParameterType)
    // todo: rename generic* to type*
    data_mods : list(DataDefMod)
    generic_params : list((TypeParameter * TYPE * TYPE)) // TypeParameter, upper bound, lower bound
    generic_param : TypeParameter -> (TYPE * TYPE) // generic parameter -> upper bound, lower bound
    generic_arg : TypeParameter -> TYPE   // generic argument, parameter -> instantiated type
    type_args : list(TYPE)
    context : string -> CONTEXT

signature
  name-resolution
    labels
      INHERIT // Between data scopes
      DATA // merged scope to data scope. Is prioritized over P when looking up data types.
      P // Multi-purpose "Parent" edge. Uses:
        // - Scopes within functions (each declaration creates a new scope, as do list comprehensions)
        // - Functions to file, files to root
        // - data type with instantiated type parameters to data type with generic type parameters
      I // import module tree to root module tree
      FILE // module tree scope to file scope
      MODULE_TREE_ROOT // points to a module tree root. Starts in root scope for
                       // module structure or file scope for pseudo modules from imports

rules // Logic functions

  /**
   * Logical and. Called andB because `and` is apparently a keyword.
   */
  andB : BOOLEAN * BOOLEAN -> BOOLEAN
  andB(FALSE(), _) = FALSE().
  andB(_, FALSE()) = FALSE().
  andB(TRUE(), TRUE()) = TRUE().
  andB(a, b) = FALSE() :- false | error $[Bug in Statix spec: no matching rule for andB([a], [b]).].

  /**
   * return TRUE() if all values in the list are TRUE().
   */
  allTrue : list(BOOLEAN) -> BOOLEAN
  allTrue([]) = TRUE().
  allTrue([FALSE()|_]) = FALSE().
  allTrue([TRUE()|rest]) = allTrue(rest).

  orB : BOOLEAN * BOOLEAN -> BOOLEAN
  orB(TRUE(), _) = TRUE().
  orB(_, TRUE()) = TRUE().
  orB(FALSE(), FALSE()) = FALSE().
  orB(a, b) = FALSE() :- false | error $[Bug in Statix spec: no matching rule for orB([a], [b]).].

  notB : BOOLEAN -> BOOLEAN
  notB(TRUE()) = FALSE().
  notB(FALSE()) = TRUE().

rules // Declararing and resolving

  // For declaring and resolving modules in relation `mod` and `mod_wip`, see
  // statics/module!declareModule and statics/module!resolveModuleList

  /**
   * Declare a value [name] with type [TYPE] and ValKind [kind] in scope [s],
   * set the type on the value name.
   */
  declareVal : scope * string * TYPE * ValKind
  declareVal(s, name, type, kind) :-
    declareValWithoutSettingType(s, name, type, kind),
    @name.type := type.

  /**
   * Declare a value [name] with type [TYPE] and ValKind [kind] in scope [s],
   * do not set the type on the value name.
   */
  declareValWithoutSettingType : scope * string * TYPE * ValKind
  declareValWithoutSettingType(s, name, type, kind) :-
    !val[name, type] in s,
    @name.kind := kind,
    resolveVal(s, name) == [_]
      | error $[Duplicate definition of value [name]].

  /**
   * Resolve a value [name] in scope [s]
   */
  resolveVal : scope * string -> list((path * (string * TYPE)))
  resolveVal(s, name) = occs :-
    query val
      filter P*
         and { name' :- name' == name }
         min and false
          in s |-> occs.

  /**
   * resolveUniqueVal(s, name) = (ty, resolved)
   * Resolves a value [name] to a unique type [ty] in scope [s].
   * Sets a type and a reference to the declaration on this name.
   * Gives a undeclared value error if no declaration is found.
   * Does not give an error if there are multiple definitions as there are
   * already errors on the declarations.
   * [resolved] indicated whether [name] resolved to a single declaration.
   * If it did not (resolved == FALSE()), [ty] will be an unbound variable.
   * See also resolveVal
   */
  resolveUniqueVal : scope * string -> (TYPE * BOOLEAN)
  resolveUniqueVal(s, name) = resolveUniqueVal_1(resolveVal(s, name), name).

    resolveUniqueVal_1 : list((path * (VALID * TYPE))) * VALID -> (TYPE * BOOLEAN)
    resolveUniqueVal_1([], name) = (_, FALSE()) :-
      false | error $[Cannot resolve [name]] @name.

    resolveUniqueVal_1([(_, (name', ty))], name) = (ty, TRUE()) :-
      @name.ref := name',
      @name.type := ty.

    // If there are multiple, the declaration will give an error.
    resolveUniqueVal_1([_,_|_], _) = (_, FALSE()).


  /**
   * Declare a function [name] with type [TYPE] in scope [s], set the type on
   * the function name.
   */
  declareFunc : scope * string * (scope * TYPE) * FuncImplKind
  declareFunc(s, name, (s_func, type), kind) :-
    !func[name, (s_func, type)] in s,
    resolveFunc(s, name) == [_] | error $[Duplicate function [name]] @name,
    @name.type := type,
    @name.kind := kind,
    @name.qualifiedModule := getQualifiedModule(getFileIfExists(s), s).

    getQualifiedModule : list(scope) * scope -> list(MODID)
    getQualifiedModule([], s) = ["<ERROR>"] :- false | error $[BUG: cannot get file from scope [s]].
    getQualifiedModule([s_file], _) = moduleToList(module) :-
      getContext(s_file, "module_name_qualified") == ContextModule(module) | error $[BUG: cannot get qualified module name from [s_file]].
    getQualifiedModule([_,_|_], s) = ["<ERROR>"] :- false | error $[BUG: multiple files from scope [s]].

  /**
   * declareFuncImport(s, import_as_name, import_name, (decl_name, type))
   * Declare an imported function [import_as_name] with type [type] in scope [s].
   * Set type on [import_name] and [import_as_name] and reference on [import_as_name].
   */
  declareFuncImport : scope * FUNCID * FUNCID * (FUNCID * (scope * TYPE))
  declareFuncImports maps declareFuncImport(*, *, *, list(*))
  declareFuncImport(s, import_as_name, import_name, (decl_name, (s_func, type))) :-
    !func[import_as_name, (s_func, type)] in s,
    resolveFunc(s, import_as_name) == [_]
      | error $[Duplicate function [import_as_name]] @import_as_name,
    @import_as_name.type := type,
    @import_as_name.renamed_from := import_name,
    @import_name.ref := decl_name.

  /**
   * declareLocalFunc(s, name, type)
   * Declare a local function [name] with type [type] in scope [s]
   */
  declareLocalFunc : scope * string * (scope * TYPE)
  declareLocalFunc(s, name, (s_func, type)) :- !func_local[name, (s_func, type)] in s.

  /**
   * resolveLocalFunc(s, name)
   * Resolve local function [name] in scope [s] to a list of function types.
   * Used when declaring imports to avoid circular dependencies.
   */
  resolveLocalFunc : scope * FUNCID -> list((path * (FUNCID * (scope * TYPE))))
  resolveLocalFunc(s, name) = occs :-
    query func_local
      filter P*
         and { name' :- name' == name }
         min and false
          in s |-> occs.

  /**
   * Resolve a function [name] in scope [s]
   */
  resolveFunc : scope * FUNCID -> list((path * (FUNCID * (scope * TYPE))))
  resolveFunc(s, name) = occs :-
    query func
      filter (P|INHERIT)*
         and { name' :- name' == name }
         min $ < INHERIT
         and true
          in s |-> occs.

  /**
   * declareData(s, name, s_data)
   * Declare a datatype [name] with scope [s_data] in scope [s], set the type
   * on the data name.
   */
  declareData : scope * TYPEID * scope
  declareData(s, name, s_data) :-
    !data[name, s_data] in s,
    resolveData(s, name) == [_]
      | error $[Duplicate definition of Datatype [name]] @name,
    @name.type := DataType(s_data).

  /**
   * declareDataImport(s, import_as_name, import_name, (decl_name, s_data))
   * Declare a datatype [decl_name] with scope [s_data] in scope [s].
   * Set reference and type on [import_name].
   */
  declareDataImport : scope * TYPEID * TYPEID * (TYPEID * scope)
  declareDataImports maps declareDataImport(*, *, *, list(*))
  declareDataImport(s, import_as_name, import_name, (decl_name, s_data)) :-
    !data[import_as_name, s_data] in s,
    resolveData(s, import_as_name) == [_]
      | error $[Duplicate definition of Datatype [import_as_name]] @import_as_name,
    @import_as_name.type := DataType(s_data),
    @import_as_name.renamed_from := import_name,
    @import_name.ref := decl_name.

  /**
   * declareLocalData(s, name, s_data)
   * Declare a local datatype [name] with scope [s_data] in scope [s]
   */
  declareLocalData : scope * string * scope
  declareLocalData(s, name, s_data) :- !data_local[name, s_data] in s.

  /**
   * resolveLocalData(s, name)
   * Resolve data [name] in scope [s] to a list of data scopes.
   * Used when declaring imports to avoid circular dependencies.
   */
  resolveLocalData : scope * string -> list((path * (string * scope)))
  resolveLocalData(s, name) = occs :-
    query data_local
      filter DATA? P*
         and { name' :- name' == name }
         min DATA < P and false
          in s |-> occs.

  /**
   * Resolve data [name] in scope [s] to a list of data scopes
   */
  resolveData : scope * string -> list((path * (string * scope)))
  resolveData(s, name) = occs :-
    query data
      filter DATA? P*
         and { name' :- name' == name }
         min DATA < P and false
          in s |-> occs.

  /**
   * resolveUniqueData(s, name) = s_data
   * Resolve data [name] to a unique data scope [s_data] in scope [s].
   * [resolved] is TRUE if [name] resolved to a unique data type. If not,
   * [s_data] will be a dummy scope.
   */
  resolveUniqueData : scope * TYPEID -> (BOOLEAN * scope)
  resolveUniqueData(s, name) = resolveUniqueData_1(s, resolveData(s, name), name).

    resolveUniqueData_1 : scope * list((path * (TYPEID * scope))) * TYPEID -> (BOOLEAN * scope)
    resolveUniqueData_1(s, [], name) = (FALSE(), emptyScope(s)) :- false | error $[Undefined data [name]] @name.
    resolveUniqueData_1(_, [(_, (name', s_data))], name) = (TRUE(), s_data) :-
      @name.ref := name',
      @name.type := DataType(s_data).
    resolveUniqueData_1(s, [_,_|_], _) = (FALSE(), emptyScope(s)).


  /**
   * getTypeParams(s_data) = params
   * Get the list of generic parameters of datatype scope [s_data].
   * [s_data] can be the generic definition or an instance.
   */
  getTypeParams : scope -> list((TypeParameter * TYPE * TYPE))
  getTypeParams(s_data) = params :-
    query generic_params
      filter P*
         min $ < P and true
          in s_data |-> [(_, params)]
      | error $[BUG: Could not get generic parameters from [s_data]].

  /**
   * declareTypeParam(s, name, upper_bound) = param
   * Declares a generic parameter [param] constructed from [name] and [s] with
   * upper bound [upper_bound] in [s].
   * Checks for duplicates and sets the type on [name].
   */
  declareTypeParam : scope * TYPEID * TYPE * TYPE -> TypeParameter
  declareTypeParam(s, name, upper_bound, lower_bound) = param :-
    !generic_param[param@TypeParameter(s, name), (upper_bound, lower_bound)] in s,
    resolveTypeParam(s, name) == [_]
      | error $[Duplicate definition of generic parameter [name]],
    @name.type := wrapBound((param, upper_bound, lower_bound)).

  /**
   * resolveTypeParam(param, name) = occs
   * Resolve a generic parameter [name] to its declaration(s) [occs] from scope
   * [s].
   * Does not check if the resolved declarations are unique.
   */
  resolveTypeParam : scope * TYPEID -> list((path * (TypeParameter * (TYPE * TYPE))))
  resolveTypeParam(s, name) = occs :-
    query generic_param
      filter P?
         and { param' :- TypeParameter(_, name) == param' }
         min and false
          in s |-> occs.

  /**
   * declareTypeArg(s, param, ty)
   * Declare a type argument with type [ty] for type parameter [param] in
   * scope [s].
   * This checks for and gives a [BUG] error on duplicates type arguments.
   */
  declareTypeArg : scope * TypeParameter * TYPE
  declareTypeArgs maps declareTypeArg(*, list(*), list(*))
  declareTypeArg(s, param@TypeParameter(_, name), ty) :-
    !generic_arg[param, ty] in s,
    resolveTypeArg(s, param) == [_]
      | error $[BUG: multiple arguments for type parameter [name]].

  /**
   * resolveTypeArgFromNameOnly(s, name, follow_inherit) = occs
   * Resolve [name] to type argument occurences [occs] in [s].
   * May return any number of type arguments.
   * If [follow_inherit] is TRUE it follows both P and INHERIT edges, otherwise
   * only P edges.
   */
  resolveTypeArgFromNameOnly : scope * TYPEID * BOOLEAN -> list((path * (TypeParameter * TYPE)))
  resolveTypeArgFromNameOnly(s, name, FALSE()) = occs :-
    query generic_arg
      filter P*
         and { TypeParameter(_, name') :- name == name' }
         min $ < P
          in s |-> occs.
  resolveTypeArgFromNameOnly(s, name, TRUE()) = occs :-
    query generic_arg
      filter (INHERIT|P)*
         and { TypeParameter(_, name') :- name == name' }
         min $ < P, $ < INHERIT
          in s |-> occs.

  /**
   * resolveTypeArg(s, name) = occs
   * Resolve [name] to type argument occurences [occs] in [s].
   * May return any number of type arguments. Follows P and INHERIT edges.
   */
  resolveTypeArg : scope * TypeParameter -> list((path * (TypeParameter * TYPE)))
  resolveTypeArg(s, param) = occs :-
    query generic_arg
      filter (INHERIT|P)*
         and { param' :- param == param' }
         min $ < P, $ < INHERIT
          in s |-> occs.

  /**
   * resolveUniqueTypeArg(s, param, error_node) = instantiation
   * Resolve type parameter [param] to its instantiated type [instantiation] in
   * [s]. Gives an error if the type argument is undefined or if there are
   * multiple type arguments.
   * See https://github.com/MeAmAnUsername/pie/issues/187 for the string
   * argument.
   */
  resolveUniqueTypeArg : scope * TypeParameter * string -> TYPE
  resolveUniqueTypeArgs maps resolveUniqueTypeArg(*, list(*), *) = list(*)
  resolveUniqueTypeArg(s, param@TypeParameter(_, name), error_node) =
    resolveUniqueTypeArg_1(name, resolveTypeArg(s, param), error_node).

    resolveUniqueTypeArg_1 : TYPEID * list((path * (TypeParameter * TYPE))) * string -> TYPE
    resolveUniqueTypeArg_1(name, [], error_node) = _ :- false | error $[Undefined type argument [name]] @error_node.
    resolveUniqueTypeArg_1(_, [(_, (_, ty))], _) = ty.
    resolveUniqueTypeArg_1(name, [_|_], error_node) = _ :- false | error $[Duplicate type argument [name]] @error_node.


  /**
   * setContext(s, name, context)
   * Set a context value [context] named [name] in scope [s].
   */
  setContext : scope * string * CONTEXT
  setContext(s, name, context) :-
    !context[name, context] in s,
    getContext(s, name) == context | error $[Context value [name] is already set in [s]].

  /**
   * getContext(s, name) -> context
   * Get a context value [context] named [name] from scope [s].
   */
  getContext : scope * string -> CONTEXT
  getContext(s, name) = context :- {occs}
    query context
      filter P*
         and { name' :- name' == name }
         min $ < P
         and true
          in s |-> occs,
    occs == [(_, (_, context))],
    try { occs != [] } | error $[cannot get context value [name] in [s]].

  /**
   * copyContext(s, name)
   * Resolves context value [name] in the parent scopes of scope [s] and
   * declares it in [s] as well.
   */
  copyContext : scope * string
  copyContext(s, name) :-
    {occs value}
    query context
      filter P+
         and { name' :- name' == name }
         min $ < P
         and true
          in s |-> occs,
    occs == [(_, (_, value))] | error $[BUG: cannot get context value [name] from parents of [s]],
    !context[name, value] in s.
