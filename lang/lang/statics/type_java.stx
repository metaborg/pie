module statics/type_java

imports

  statics/base
  statics/type
  statics/type_common
  statics/common

rules

  /**
   * isSubsignature(s_renamings, signature1, signature2) = is_subsignature
   * Checks if [signature1] is a subsignature of [signature2].
   * Implementation of the definition from
   * https://docs.oracle.com/javase/specs/jls/se7/html/jls-8.html#jls-8.4.2
   * [s_renamings] maps some type parameters from [signature2] to something
   * else. This can be used for e.g. renaming type parameters that are defined
   * by a data type instead of the function.
   */
  isSubsignature : scope * scope * TYPE * scope * TYPE -> BOOLEAN
  isSubsignature(s_renamings, s_func1, signature1@FuncType(type_params1, formal_params1, return_type1),
    s_func2, signature2@FuncType(type_params2, formal_params2, return_type2)) =
      orB(isSignatureEqual(s_renamings, s_func1, signature1, s_func2, signature2),
        isSignatureEqualToErased(s_renamings, signature1, signature2)).
  isSubsignature(_, _, _, _, _) = FALSE() :-
    false | error $[BUG: cannot call isSubsignature with types other than two FuncTypes].


  /**
   * isSignatureEqual(s, signature1, signature2) = is_signature_equal
   * Checks if two signatures are equal.
   * Implementation of the definition from
   * https://docs.oracle.com/javase/specs/jls/se7/html/jls-8.html#jls-8.4.2
   * [s_renamings] maps some type parameters from [signature2] to something
   * else. This can be used for e.g. renaming type parameters that are defined
   * by a data type instead of the function.
   */
  isSignatureEqual : scope * scope * TYPE * scope * TYPE -> BOOLEAN
  isSignatureEqual(s_renamings, s_func1, signature1@FuncType(type_params1, formal_params1, _),
    s_func2, signature2@FuncType(type_params2, formal_params2, _)) =
      isSignatureEqual_1(type_params_equal_bounds, s_renamings, s_func1, signature1, s_func2, signature2) :-
        isTypeParameterBoundsEqual(type_params1, type_params2) == type_params_equal_bounds.

    isSignatureEqual_1 : BOOLEAN * scope * scope * TYPE * scope * TYPE -> BOOLEAN
    isSignatureEqual_1(FALSE(), _, _, _, _, _) = FALSE().
    isSignatureEqual_1(TRUE(), s_renamings, s_func1, tmp1@FuncType(type_params1, formal_params1, _),
      s_func2, tmp2@FuncType(type_params2, formal_params2, _)) = params_equal :-
      {s_renamings_all renamed_params2}
      renameAll(s_renamings, s_func2, type_params2, s_func1, type_params1) == s_renamings_all,
      instantiateTypeArgsInAll(s_renamings_all, formal_params2) == renamed_params2,
      isAllInvariant(formal_params1, renamed_params2) == params_equal.

    isTypeParameterBoundsEqual : list(GenericParamType) * list(GenericParamType) -> BOOLEAN
    isTypeParameterBoundsEqual([], []) = TRUE().
    isTypeParameterBoundsEqual([_|_], []) = FALSE().
    isTypeParameterBoundsEqual([], [_|_]) = FALSE().
    isTypeParameterBoundsEqual([param1|params1], [param2|params2]) = andB(
      isTypeParameterBoundEqual(param1, param2),
      isTypeParameterBoundsEqual(params1, params2)
    ).

    isTypeParameterBoundEqual : GenericParamType * GenericParamType -> BOOLEAN
    isTypeParameterBoundEqual(GenericParamType(_, ub1, lb1), GenericParamType(_, ub2, lb2)) =
      andB(
        isInvariant(typeOrTopType(ub1), typeOrTopType(ub2)),
        isInvariant(typeOrBottomType(lb1), typeOrBottomType(lb2))).

    // create a scope s_renaming that maps generic parameters from type_params1 to type_params2
    // Overrides renamings of s_pre_existing.
    // requires that both lists have the same lengths
    renameAll : scope * scope * list(GenericParamType) * scope * list(GenericParamType) -> scope
    renameAll(s_pre_existing, s_type_params1, type_params1, s_type_params2, type_params2) = s_renaming :-
      new s_renaming,
      s_renaming -P-> s_pre_existing,
      renameAll_1(s_renaming, s_type_params1, type_params1, s_type_params2, type_params2).

      rename : scope * scope * GenericParamType * scope * GenericParamType
      renameAll_1 maps rename(*, *, list(*), *, list(*))
      rename(s, s_param1, GenericParamType(name1, _, _), s_param2, GenericParamType(name2, _, _)) :-
        {type}
        type == GenericParameter2TYPE(GenericParameter(s_param2, name2)),
        declareTypeArg(s, GenericParameter(s_param1, name1), type).

  /**
   * isSignatureEqualToErased(s_renamings, signature1@FuncType,
   *   signature2@FuncType) = is_equal
   * Checks if [signature1] is equal to [signature2] after erasure of
   * [signature2].
   * Attention: [signature2] should _not_ be erased before calling this
   * function.
   */
  isSignatureEqualToErased : scope * TYPE * TYPE -> BOOLEAN
  isSignatureEqualToErased(_, FuncType([_|_], _, _), _) = FALSE().
  isSignatureEqualToErased(s_renamings, FuncType([], formal_params1, _), FuncType(_, formal_params2, _)) =
    isSignatureEqualToErased_1(param_counts_equal, s_renamings, formal_params1, formal_params2) :-
      typeListsLengthsEqual(formal_params1, formal_params2) == param_counts_equal.
  isSignatureEqualToErased(_, _, _) = FALSE() :-
    false | error $[BUG: Cannot call isSignatureEqualToErased with types other than two FuncTypes].

    isSignatureEqualToErased_1 : BOOLEAN * scope * list(TYPE) * list(TYPE) -> BOOLEAN
    isSignatureEqualToErased_1(FALSE(), _, _, _) = FALSE().
    isSignatureEqualToErased_1(TRUE(), s_renamings, formal_params1, formal_params2) =
      allTrue(isTypesEqualToErased(formal_params1, instantiateTypeArgsInAll(s_renamings, formal_params2))).

    isTypeEqualToErased : TYPE * TYPE -> BOOLEAN
    isTypesEqualToErased maps isTypeEqualToErased(list(*), list(*)) = list(*)
    isTypeEqualToErased(UnitType(), UnitType()) = TRUE().
    isTypeEqualToErased(BoolType(), BoolType()) = TRUE().
    isTypeEqualToErased(IntType(), IntType()) = TRUE().
    isTypeEqualToErased(StrType(), StrType()) = TRUE().
    isTypeEqualToErased(PathType(), PathType()) = TRUE().
    isTypeEqualToErased(NullType(), NullType()) = TRUE() :-
      false | error $[BUG?: checking if null type is equal to an erased null type].
    isTypeEqualToErased(TopType(), TopType()) = TRUE() :-
      false | error $[BUG?: checking if top type is equal to an erased top type].
    isTypeEqualToErased(BottomType(), BottomType()) = TRUE() :-
      false | error $[BUG?: checking if bottom type is equal to an erased bottom type].
    isTypeEqualToErased(NullableType(T1), NullableType(T2)) = isTypeEqualToErased(T1, T2).
    isTypeEqualToErased(NullableType(T1), T2) = isTypeEqualToErased(T1, T2).
    isTypeEqualToErased(T1, NullableType(T2)) = isTypeEqualToErased(T1, T2).
    isTypeEqualToErased(ListType(_), ListType(_)) = TRUE().
    isTypeEqualToErased(EmptyListType(), EmptyListType()) = TRUE() :-
      false | error $[BUG?: checking if empty list type is equal to an erased empty list type].
    isTypeEqualToErased(TupleType(Ts1), TupleType(Ts2)) =
      typeListsLengthsEqual(Ts1, Ts2).
    isTypeEqualToErased(FuncType(_, params1, _), FuncType(_, params2, _)) =
      typeListsLengthsEqual(params1, params2) :-
        false | error $[BUG?: checking if function type is equal to an erased function type].
    isTypeEqualToErased(DataType(s_data1), DataType(s_data2)) =
      isTypeEqualToErased_dataTypeEqual(s_data_def1, s_data_def2, getTypeArgs(s_data1)) :-
        findGenericDef(s_data1) == s_data_def1,
        findGenericDef(s_data2) == s_data_def2.
    isTypeEqualToErased(T, param@GenericParameter2TYPE(_)) =
      isTypeEqualToErased(T, upper_bound) :-
        getBounds(param) == (upper_bound, _).
    isTypeEqualToErased(T, WildcardType(upper_bound, _)) = isTypeEqualToErased(T, typeOrTopType(upper_bound)) :-
      false | error $[BUG?: checking if [T] is equal to an erased wildcard].
    isTypeEqualToErased(T1, T2) = FALSE().

    // only equal if generic defs are the same and the first instance has no type arguments.
    isTypeEqualToErased_dataTypeEqual : scope * scope * list((GenericParameter * TYPE)) -> BOOLEAN
    isTypeEqualToErased_dataTypeEqual(s, s, []) = TRUE().
    isTypeEqualToErased_dataTypeEqual(_, _, _) = FALSE().

  /**
   * eraseType(T) = erased_type
   * erases TYPE [T] to TYPE [erased_type].
   * This removes all generic arguments and makes it nullable.
   * This also takes the upper bound of generic variables and wildcard.
   * Because there are no specific erased types, this just replaces everything
   * with UnitType if outright removing it is not possible.
   */
  eraseType : TYPE -> TYPE
  eraseTypes maps eraseType(list(*)) = list(*)
  eraseType(T) = makeNullable(eraseType_1(T)).

    eraseType_1 : TYPE -> TYPE
    eraseType_1(T) = T.
    eraseType_1(DataType(s_data)) = DataType(findGenericDef(s_data)).
    eraseType_1(NullableType(T)) = NullableType(eraseType_1(T)).
    eraseType_1(ListType(T)) = ListType(UnitType()).
    eraseType_1(TupleType(Ts)) = TupleType(replaceWithUnit(Ts)).
      getUnit : TYPE -> TYPE
      replaceWithUnit maps getUnit(list(*)) = list(*)
      getUnit(_) = UnitType().
    eraseType_1(SupplierType(_)) = SupplierType(UnitType()).
    eraseType_1(FuncType(_, params, _)) = FuncType([], replaceWithUnit(params), UnitType()).
    eraseType_1(WildcardType(ub, lb)) = typeOrTopType(ub).
    eraseType_1(T@GenericParameter2TYPE(_)) = eraseType(ub) :-
      getBounds(T) == (ub, _).
