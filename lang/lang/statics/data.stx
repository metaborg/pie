module statics/data

imports

  signatures/constructors-sig
  signatures/data-sig
  statics/base
  statics/type
  statics/type_common
  statics/type_java
  statics/common
  statics/func_def

rules

  dataDefOk : scope * DataDef
  dataDefOk(s, DataDef(DataDefMods(mods), name, type_params, super, impl)) :- {s_data_def}
    dataDefModsOk(mods),
    new s_data_def,
    !root[getRoot(s)] in s_data_def,
    setFileIfExists(s, s_data_def),
    setContext(s_data_def, "data_name_unqualified", ContextName(name)),
    !data_mods[mods] in s_data_def,
    typesOfTypeParams(s, s_data_def, type_params) == _,
    declareData(s, name, s_data_def),
    declareLocalData(s, name, s_data_def),
    superOk(s, s_data_def, super, name),
    dataImplOk(s, s_data_def, impl).

  dataDefModsOk : list(DataDefMod)
  dataDefModsOk([]).
  dataDefModsOk([mod|mods1]) :- {mods2}
    dataDefModsOk_1(mods1, mod) == mods2,
    dataDefModsOk(mods2).

    // checks that mods list does not contain a duplicate modifier
    // returns list with duplicates removed
    dataDefModsOk_1 : list(DataDefMod) * DataDefMod -> list(DataDefMod)
    dataDefModsOk_1([], _) = [].
    dataDefModsOk_1([mod|mods], mod) = mods_out :-
      dataDefModsOk_1(mods, mod) == mods_out,
      false | error $[Duplicate modifier [mod]] @mod.
    dataDefModsOk_1([mod1|mods], mod2) = [mod1|mods_out] :-
      dataDefModsOk_1(mods, mod2) == mods_out.

  superOk : scope * scope * SuperType * string
  superOk(s, s_data_def, NoSuperType(), name).
  superOk(s, s_data_def, SuperType(data_ty@DataTy(_, super_name, _)), name) :- {s_super occs}
    typeOfDataTy(s, s_data_def, data_ty, FALSE(), []) == DataType(s_super),
    s_data_def -INHERIT-> s_super,
    inherits(s_super, s_data_def) == FALSE() | error $[Cannot (indirectly) inherit from self. [super_name] is already an (indirect) subtype of [name]] @super_name,
    query func filter e in s_data_def |-> occs,
    superOk_1s(s_data_def, s_super, occs).

    // check that all functions of current are valid overrides of super.
    superOk_1 : scope * scope * (path * (FUNCID * (scope * TYPE)))
    superOk_1s maps superOk_1(*, *, list(*))
    superOk_1(s_data_def, s_super, (_, (name, func_ty))) :- {occs}
      resolveFunc(s_super, name) == occs,
      superOk_2s(s_data_def, s_super, func_ty, occs, name).

    // Check that function is either a valid override or not an override
    superOk_2 : scope * scope * (scope * TYPE) * (path * (FUNCID * (scope * TYPE))) * FUNCID
    superOk_2s maps superOk_2(*, *, *, list(*), *)
    superOk_2(s_data_def, s_super, (s_func, signature), (_, (name_super, (s_func_super, signature_super))), name) :-
      {is_subsignature}
      isSubsignature(s_data_def, s_func, signature, s_func_super, signature_super) == is_subsignature,
      superOk_3(is_subsignature, s_data_def, name, s_func, signature, name_super, s_func_super, signature_super).


    // if function is an override, require return type to be compatible.
    // if function is not an override, check that it doesn't collide with
    // another function after erasure.
    superOk_3 : BOOLEAN * scope * TYPEID * scope * TYPE * TYPEID * scope * TYPE
    superOk_3(TRUE(), s_data_def, name, s_func, FuncDefType(type_params, _, ret_ty),
        name_super, s_func_super, FuncDefType(type_params_super, _, ret_ty_super)) :-
      {s_renamings instantiated_ret_ty_super}
      superOk_3_rename_if_not_erased(s_data_def, s_func, type_params, s_func_super, type_params_super) == s_renamings,
      instantiateTypeArgs(s_renamings, ret_ty_super) == instantiated_ret_ty_super,
      assignableTo(ret_ty, instantiated_ret_ty_super, ExpressionKind())
        | error $[Type mismatch: incompatible return type for override] @name,
      @name.ref := name_super.

      // renames super method type parameters to sub method type parameters
      // unless sub method has no type parameters, in which case it may have
      // been classified as an override due to type erasing the super method.
      superOk_3_rename_if_not_erased : scope * scope * list(TypeParamType) * scope * list(TypeParamType) -> scope
      superOk_3_rename_if_not_erased(s_data_def, _, [], _, _) = s_data_def.
      superOk_3_rename_if_not_erased(s_data_def, s_func, type_params@[_|_], s_func_super, type_params_super) =
        renameAll(s_data_def, s_func_super, type_params_super, s_func, type_params).

    superOk_3(FALSE(), _, name, _, FuncDefType(_, params, _), _, _, FuncDefType(_, super_params, _)) :-
      {erased_params erased_super_params}
      eraseTypes(params) == erased_params,
      eraseTypes(super_params) == erased_super_params,
      isAllAssignableTo(erased_params, erased_super_params, InvariantKind()) == FALSE()
        | error $[[name] collides with method in super type: this method does not override method in supertype but has the same signature after type erasure].

  dataImplOk : scope * scope * DataImpl
  dataImplOk(s, s_data, JavaDataImpl(java_name, members)) :-
    setContext(s_data, "foreign_java_impl", ContextName(java_name)),
    javaDataMembersOk(s, s_data, members).

  javaDataMemberOk : scope * scope * JavaDataMemb
  javaDataMembersOk maps javaDataMemberOk(*, *, list(*))
  javaDataMemberOk(s, s_data, JavaFuncDef(func_head@FuncHead(_, _, Params(params), _))) :-
    {s_func name ty}
    typeOfFuncHead(s, s_data, func_head) == (s_func, name, ty),
    declareFunc(s_data, name, (s_func, ty), JavaForeignFuncKind()),
    warnForUselessAnonymousParameters(params).


  getDataName : scope -> TYPEID
  getDataName(s) = name :-
    getContext(s, "data_name_unqualified") == ContextName(name).
