module statsem/func_body

imports

  signatures/-

rules // Expression composition

  [[ Block(exps) ^ (s, ty_func_def) : last_exp_ty ]] :=
    new s_block,
    s_block -P-> s,
    BlockExps [[ exps ^ (s_block, ty_func_def) : last_exp_ty ]].

  BlockExps [[ [exp|exps@[_|_]] ^ (s, ty_func_def) : last_exp_ty ]] :=
    [[exp ^ (s, ty_func_def) : _ ]],
    BlockExps [[ exps ^ (s, ty_func_def) : last_exp_ty ]].

  BlockExps [[ [exp] ^ (s, ty_func_def) : last_exp_ty ]] :=
    [[ exp ^ (s, ty_func_def) : last_exp_ty ]].

  [[ EmptyBlock() ^ (s, _) : UnitTy() ]] := true.

rules // Unary expressions

  [[ ToNullable(exp) ^ (s, ty_func_def) : NullableTy(ty) ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    ty != NullableTy(_) | error $[Type mismatch: expected non-nullable type, got [ty]] @ exp.
    
  [[ ToNonNullable(exp) ^ (s, ty_func_def) : ty_inner ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    ty == NullableTy(ty_inner) | error $[Type mismatch: expected nullable type, got [ty]] @ exp.

  [[ Not(exp) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    ty == BoolTy() | error $[Type mismatch: expected boolean type, got [ty]] @ exp.

rules // Binary expressions

  // TODO: should use LUB to work both ways. However, that requires separating nullability into a property 
  // of a type (by creating a tuple for each type), because otherwise LUB could always result into the top 
  // type TopTy(), instead of failing.
  [[ t@Eq(exp_l, exp_r) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp_l ^ (s, ty_func_def) : ty_l ]],
    [[ exp_r ^ (s, ty_func_def) : ty_r ]],
    ty_r <? ty_l | error $[Type mismatch: cannot compare [ty_l] to [ty_r]] @ t.
  [[ t@Neq(exp_l, exp_r) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp_l ^ (s, ty_func_def) : ty_l ]],
    [[ exp_r ^ (s, ty_func_def) : ty_r ]],
    ty_r <? ty_l | error $[Type mismatch: cannot compare [ty_l] to [ty_r]] @ t.

  [[ t@Lor(exp_l, exp_r) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp_l ^ (s, ty_func_def) : ty_l ]],
    ty_l <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_l]] @ exp_l,
    [[ exp_r ^ (s, ty_func_def) : ty_r ]],
    ty_r <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_r]] @ exp_r.
  [[ t@Land(exp_l, exp_r) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp_l ^ (s, ty_func_def) : ty_l ]],
    ty_l <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_l]] @ exp_l,
    [[ exp_r ^ (s, ty_func_def) : ty_r ]],
    ty_r <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_r]] @ exp_r.

  [[ t@Add(exp_l, exp_r) ^ (s, ty_func_def) : ty ]] :=
    [[ exp_l ^ (s, ty_func_def) : ty_l ]],
    [[ exp_r ^ (s, ty_func_def) : ty_r ]],
    ty is addType of (ty_l, ty_r) | error $[Type mismatch: cannot add [ty_l] to [ty_r]] @ t.

rules // Control flow

  [[ If(exp_cond, exp) ^ (s, ty_func_def) : ty ]] :=
    [[ exp_cond ^ (s, ty_func_def) : ty_cond ]],
    ty_cond <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_cond]] @ exp_cond,
    [[ exp ^ (s, ty_func_def) : ty ]].

  [[ e@IfElse(exp_cond, exp_true, exp_false) ^ (s, ty_func_def) : ty_false ]] :=
    [[ exp_cond ^ (s, ty_func_def) : ty_cond ]],
    ty_cond <? BoolTy() | error $[Type mismatch: expected boolean type, got [ty_cond]] @ exp_cond,
    [[ exp_true ^ (s, ty_func_def)  : ty_true ]],
    [[ exp_false ^ (s, ty_func_def) : ty_false ]],
    // TODO: this should calculate the LUB of the expression types
    ty_true <? ty_false | error $[Type mismatch: expected [ty_false], got [ty_true]] @ e.


  [[ ListComprehension(exp_map, binder, exp_list) ^ (s, ty_func_def) : ty ]] :=
    [[ exp_list ^ (s, ty_func_def) : ty_list ]],
    ty_list == ListTy(ty_list_inner) | error $[Type mismatch: expected list type, got [ty_list]] @ exp_list,
    new s_comprehension,
    s_comprehension -P-> s,
    Binder [[ binder ^ (s_comprehension, ty_list_inner) : ty_dec ]],
    [[ exp_map ^ (s_comprehension, ty_func_def) : ty_map ]],
    ty == ListTy(ty_map).

rules // Binding

  [[ ValDec(binder, exp) ^ (s, ty_func_def) : UnitTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    Binder [[ binder ^ (s, ty) : ty_dec ]].

rules // References
    
  [[ Ref(id_var) ^ (s, _) : ty ]] :=
    Var{id_var} -> s,
    Var{id_var} |-> v,
    v : ty.


  [[ Call(id_func, args) ^ (s, ty_func_def) : ty_out ]] :=
    Func{id_func} -> s,
    Func{id_func} |-> f,
    f : ty_func,
    ty_func == FuncTy(tys_in_expected, ty_out) | error $[Type mismatch: expected function type, got [ty_func]] @ id_func,
    Map2T [[ args ^ (s, ty_func_def) : tys_in ]],
    tys_in <? tys_in_expected | error $[Type mismatch: expected [tys_in_expected], got [tys_in]] @ id_func.

  [[ QCall(exp, id_func, args) ^ (s, ty_func_def) : ty_out ]] :=
    // Get definition occurrences for primitive types
    Type{StrTy()} -> s , Type{StrTy() } |-> occ_def_str,
    Type{PathTy()} -> s, Type{PathTy()} |-> occ_def_path,
    [[ exp ^ (s, ty_func_def) : ty_data ]],
    occ is typeToOcc of (ty_data, occ_def_str, occ_def_path) | error $[Type mismatch: expected callable type, got [ty_data]] @ exp, 
    occ ?=I=> s_assoc,
    new s_imp,
    s_imp -I-> s_assoc,
    Func{id_func} -> s_imp,
    Func{id_func} |-> f,
    f : FuncTy(tys_in_expected, ty_out),
    Map2T [[ args ^ (s, ty_func_def) : tys_in ]], 
    tys_in <? tys_in_expected | error $[Type mismatch: expected [tys_in_expected], got [tys_in]] @ id_func.

rules // Path operations

  [[ RequiresPath(exp, filter, stamper) ^ (s, ty_func_def) : UnitTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp,
    [[ filter ^ (s, ty_func_def) ]],
    [[ stamper ^ (s, ty_func_def) ]].

  [[ GeneratesPath(exp, stamper) ^ (s, ty_func_def) : UnitTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp,
    [[ stamper ^ (s, ty_func_def) ]].

  [[ Modified() ^ (_, _) ]] := true.
  [[ Hash()     ^ (_, _) ]] := true.
  

  [[ ListPath(exp, filter) ^ (s, ty_func_def) : ListTy(PathTy()) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp,
    [[ filter ^ (s, ty_func_def) ]].    

  [[ WalkPath(exp, filter) ^ (s, ty_func_def) : ListTy(PathTy()) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp,
    [[ filter ^ (s, ty_func_def) ]].


  [[ Regex(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? StrTy() | error $[Type mismatch: expected string type, got [ty_exp]] @ exp.
    
  [[ Pattern(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? StrTy() | error $[Type mismatch: expected string type, got [ty_exp]] @ exp.
    
  [[ Patterns(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? ListTy(StrTy()) | error $[Type mismatch: expected string* type, got [ty_exp]] @ exp.
    
  [[ Extension(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? StrTy() | error $[Type mismatch: expected string type, got [ty_exp]] @ exp.
    
  [[ Extensions(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? ListTy(StrTy()) | error $[Type mismatch: expected string* type, got [ty_exp]] @ exp.


  [[ ReadPath(exp) ^ (s, ty_func_def) : NullableTy(StrTy()) ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp.  

  [[ ExistsPath(exp) ^ (s, ty_func_def) : BoolTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty_exp ]],
    ty_exp <? PathTy() | error $[Type mismatch: expected path type, got [ty_exp]] @ exp.  

rules // Early return and failure

  [[ Return(exp) ^ (s, ty_func_def) : UnitTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty_ret ]],
    ty_func_def == FuncTy(_, ty_ret_expected),
    ty_ret <? ty_ret_expected | error $[Type mismatch: expected [ty_ret_expected], got [ty_ret]] @ exp.

  [[ Fail(exp) ^ (s, ty_func_def) : UnitTy() ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    ty <? StrTy() | error $[Type mismatch: expected string type, got [ty]] @ exp.

rules // Literals

  [[ UnitLit()  ^ (s, _) : UnitTy() ]] := true.
  [[ BoolLit(_) ^ (s, _) : BoolTy() ]] := true.
  [[ IntLit(_)  ^ (s, _) : IntTy()  ]] := true.
  [[ NullLit()  ^ (s, _) : NullTy() ]] := true.


  [[ TupleLit(exps) ^ (s, ty_func_def) : TupleTy(tys) ]] :=
    Map2T [[ exps ^ (s, ty_func_def) : tys ]].

  [[ ListLit([]) ^ (s, _) : ListTy(TopTy()) ]] := true.

  [[ ListLit([exp|exps]) ^ (s, ty_func_def) : ListTy(ty) ]] :=
    // TODO: this should calculate the LUB of the expression types.
    [[ exp ^ (s, ty_func_def) : ty ]],
    CompareTypes [[ exps ^ (s, ty_func_def, ty) ]].
    
  CompareTypes [[ [exp|exps] ^ (s, ty_func_def, ty_expected) ]] :=
    [[ exp ^ (s, ty_func_def) : ty ]],
    ty <? ty_expected | error $[Type mismatch: expected [ty_expected], got [ty]] @ exp,
    CompareTypes [[ exps ^ (s, ty_func_def, ty_expected) ]].

  CompareTypes [[ [] ^ (s, ty_func_def, ty_expected) ]] := true.

rules // Interpolated string literal

  [[ StrLit(elems) ^ (s, ty_func_def) : StrTy() ]] :=
    Map2 [[ elems ^ (s, ty_func_def) ]].
    
  [[ Str(_) ^ (s, ty_func_def) ]] := true.
  [[ StrInterpRef(exp_ref) ^ (s, ty_func_def) ]] :=
    [[ exp_ref ^ (s, ty_func_def) : _ ]].
  [[ StrInterpExp(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : _ ]].

rules // Interpolated path literal

  [[ PathLit(_, elems) ^ (s, ty_func_def) : PathTy() ]] :=
    Map2 [[ elems ^ (s, ty_func_def) ]].
    
  [[ Path(_) ^ (s, ty_func_def) ]] := true.
  [[ PathInterpRef(exp_ref) ^ (s, ty_func_def) ]] :=
    [[ exp_ref ^ (s, ty_func_def) : _ ]].
  [[ PathInterpExp(exp) ^ (s, ty_func_def) ]] :=
    [[ exp ^ (s, ty_func_def) : _ ]].
