module statsem/pie

imports

  signatures/-

signature

  namespaces Type Func Var

  name resolution

    labels          P I
    order           D < I, D < P, I < P
    well-formedness P*.I*

  constructors

    FuncTy : list(Type) * Type -> Type
    TopTy  : Type
    NullTy : Type

    JavaTypeKind : TypeKind

    JavaForeignFuncKind   : FuncKind
    JavaForeignConstructorKind : FuncKind
    PieForeignFuncKind : FuncKind
    PieFuncKind     : FuncKind

    LocalVarKind : VarKind
    InputVarKind : VarKind

  functions

    addType : (Type * Type) -> Type {
      (IntTy()  , IntTy()  ) -> IntTy(),
      (StrTy()  , _        ) -> StrTy(),
      (PathTy() , PathTy() ) -> PathTy(),
      (PathTy() , StrTy()  ) -> PathTy(),
      (ListTy(a), a        ) -> ListTy(a),
      (ListTy(a), ListTy(a)) -> ListTy(a)
    }

    typeToOcc : (Type * Occurrence * Occurrence) -> Occurrence {
      (DataTy(occ), occ_def_str, occ_def_path) -> occ,
    //  (f@FuncTy(tys_in, ty_out), occ_def_str, occ_def_path) -> f,
      (StrTy()    , occ_def_str, occ_def_path) -> occ_def_str,
      (PathTy()   , occ_def_str, occ_def_path) -> occ_def_path
    }

rules // Init

  init ^ (s_root) :=
    PieForeignFuncKind() <! PieFuncKind(), // to check if a function is a pie task
    new s_root,
    // Create definitions in type namespace for primitive types
    Type{StrTy() } <- s_root, new s_str , s_str  -P-> s_root, Type{StrTy() } =I=> s_str,
    Type{PathTy()} <- s_root, new s_path, s_path -P-> s_root, Type{PathTy()} =I=> s_path,
    // Add functions to string types
    Func{"replace"}          <- s_str,  Func{"replace"}          : FuncTy([StrTy(), StrTy()], StrTy())!,
    // Add functions to path types
    Func{"extension"}        <- s_path, Func{"extension"}        : FuncTy([], NullableTy(StrTy()))!,
    Func{"replaceExtension"} <- s_path, Func{"replaceExtension"} : FuncTy([StrTy()], PathTy())!,
    Func{"resolve"}          <- s_path, Func{"resolve"}          : FuncTy([StrTy()], PathTy())!.

rules // Program

  [[ Program(Module(moduleList), imports, defs) ^ (s_root) ]] :=
    new s_file,
    s_file -P-> s_root,
    modDecl == QualifiedModuleDeclaration{"modDecl"},
    modDecl <- s_file,
    modDecl.moduleList := moduleList !,
    modules [[ moduleList ^ (s_root, s_file) ]],
    Map1(import)[[ imports ^ (s_root, s_file) ]],
    Map1 [[ defs ^ (s_file) ]],
    distinct/name D(s_file)/Type | error "Duplicate types" @NAMES,
    distinct/name D(s_file)/Func | error "Duplicate functions" @NAMES,
    distinct/name D(s_file)/Mod  | error "Duplicate imports" @NAMES.

rules // None

  [[ None() ^ (s) ]] := true.

rules // Module scope generation

  modules [[ [first|rest] ^ (s, s_file) ]] :=
    new s_sub_module,
    Mod{first} <- s,
    Mod{first} =I=> s_sub_module,
    modules [[ rest ^ (s_sub_module, s_file) ]].

  modules [[ [] ^ (s, s_file) ]] :=
    s -I-> s_file. // s can import things from the file

rules // Import resolution & declaration

  import [[ Import(importList) ^ (s_root, s_file) ]] :=
    import [[ importList ^ (s_root, s_file) ]].

  import [[ [first|full_rest@[second|rest]] ^ (s_cur, s_file) ]] :=
    new s_next,
    Mod{first} -> s_cur,
    Mod{first} <=I= s_next,
    import [[ full_rest ^ (s_next, s_file) ]].

  import [[ [last|[]] ^ (s, s_file) ]] :=
    s_file -P-> s.

  import [[ [] ^ (s, s_file) ]] := false | error "Tried to resolve an empty qualified call".

  importFrom [[ [module|rest] ^ (s_cur, s_start) ]] :=
    new s_next,
    Mod{module} -> s_cur,
    Mod{module} <=I= s_next,
    importFrom [[ rest ^ (s_next, s_start) ]].

  importFrom [[ [] ^ (s, s_start) ]] :=
    new s_start,
    s_start -P-> s.
